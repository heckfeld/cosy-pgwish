if {[string compare test [info procs test]] == 1} then {source defs}

# source tests/defs

#
# Determine a port number 
#
set l [dp_MakeRPCServer 0 dp_CheckHost none 1]
catch {close [lindex $l 0]}
set S_PORT [expr 10+[lindex $l 1]]

#
# Setup: Start a server as another process, and use it to connect.
#
exec $argv0 -f server $S_PORT &

proc ignore args {}

proc MakeInetAddr {name} {
    set addr [dp_address create $name 0]
    set x [dp_address info $addr]
    dp_address delete $addr
    lindex $x 0
}
set hostname [exec hostname]
set hostinet [MakeInetAddr $hostname]

# This loop keeps trying to connect until it succeeds.
# Important since the exec may take a while on some machines...

dp_after 1000
set itry 0
while { [catch "dp_MakeRPCClient $hostname $S_PORT" rhost] } {
    puts stdout "Trying to connect to server..."
    incr itry
    if { $itry > 5 } {
	puts stdout "Unable to connect to server"
	break
    }
    dp_after 2000
}
catch "close $rhost"

#------------------------------------------------------------------------------
#
# Connectivity tests
#
test rpc-1.2 {creation of rpc client using hostname} {
    list [catch {dp_MakeRPCClient $hostname $S_PORT} server1] \
	 [string match file* $server1]
} {0 1}

test rpc-1.3 {creation of rpc client using internet address} {
    list [catch {dp_MakeRPCClient $hostinet $S_PORT} server2] \
	 [string match file* $server2]
} {0 1}

#------------------------------------------------------------------------------
#
# Basic message passing tests
#
test rpc-2.1 {Basic message passing} {
    dp_RPC $server1 set argv
} $S_PORT

test rpc-2.2 {basic error handling} {
    list [catch {dp_RPC $server1 set a} msg] $msg
} {1 {can't read "a": no such variable}}

test rpc-2.3 {timeout routines} {
    list [catch {dp_RPC $server1 -timeout 50 dp_after 1000} msg] $msg
} [list 1 "dp_RPC timed out on $server1"]

proc errorTest {file} {
    return "Caught error on $file"
}

test rpc-2.4 {timeout routines} {
    list [catch {dp_RPC $server1 -timeout 50 \
			  -timeoutReturn errorTest dp_after 1000} msg] $msg
} [list 0 "Caught error on $server1"]

#------------------------------------------------------------------------------
#
# RDO tests
#

test rpc-3.1 {Basic RDO} {
    dp_RPC $server1 catch {unset a}
    dp_RDO $server1 set a rpc-3.1;
    dp_RPC $server1 set a
} rpc-3.1

test rpc-3.2 {Basic RDO with callback} {
    set rdoResults 0
    dp_RDO $server1 set a rpc-3.2
    dp_RDO $server1 -callback "set rdoResults" set a
    dp_RPC $server1 set a synchronize
    set rdoResults
} rpc-3.2

test rpc-3.3 {Multiple RDOs, each with callback} {
    set RDO1 0
    set RDO2 0
    dp_RDO $server1 -callback "set RDO1" set a 3.3a
    dp_RDO $server1 -callback "set RDO2" set a 3.3b
    dp_RPC $server1 -events all set a synchronize
    list $RDO1 $RDO2
} {3.3a 3.3b}

test rpc-3.4 {Basic RDO error handling} {
    set RDOerr 0
    dp_RPC $server1 unset a
    dp_RDO $server1 -onerror "set RDOerr" set a
    dp_RPC $server1 set a synchronize
    set RDOerr
} {can't read "a": no such variable}

test rpc-3.5 {Basic RDO error generation (error will crash test)} {
    dp_RPC $server1 unset a
    catch {dp_RDO $server1 -onerror none set a}
} 0

test rpc-3.6 {RDO error handling for multiple RDO's} {
    set RDOerr1 rpc-3.6a
    set RDOerr2 rpc-3.6b
    dp_RDO $server1 -onerror none unset a
    dp_RDO $server1 -onerror none unset b
    dp_RDO $server1 -onerror "set RDOerr1" set a
    dp_RDO $server1 -onerror "set RDOerr2" set b
    dp_RPC $server1 set a synchronize
    list $RDOerr1 $RDOerr2
} {{can't read "a": no such variable} {can't read "b": no such variable}}

test rpc-3.7 {RDO callback handling for multiple RDO's} {
    set RDO1    rpc3.7a
    set RDO2    rpc3.7b
    dp_RDO $server1 set a rpc3.7c
    dp_RDO $server1 -onerror none unset b
    dp_RDO $server1 -onerror none -callback "set RDO1" set a
    dp_RDO $server1 -onerror none -callback "set RDO2" set b
    dp_RPC $server1 set a synchronize
    list $RDO1 $RDO2
} {rpc3.7c rpc3.7b}

test rpc-3.8 {Mixed RDO error and callback handling for multiple RDO's} {
    set RDOerr1 rpc3.8a
    set RDOerr2 rpc3.8b
    set RDO1    rpc3.8c
    set RDO2    rpc3.8d
    dp_RDO $server1 set a hello
    dp_RDO $server1 -onerror none unset b
    dp_RDO $server1 -callback "set RDO1" -onerror "set RDOerr1" set a
    dp_RDO $server1 -callback "set RDO2" -onerror "set RDOerr2" set b
    dp_RPC $server1 set a synchronize
    list $RDOerr1 $RDOerr2 $RDO1 $RDO2
} {rpc3.8a {can't read "b": no such variable} hello rpc3.8d}

test rpc-3.9 {mixed RPC and RDO with timeout} {
    set RDO1    rpc3.9a
    set RDO2    rpc3.9b
    dp_RDO $server1 -callback "set RDO1" set a rpc3.9c
    set rv1 [catch {dp_RPC $server1 -timeout 50 dp_after 1000} msg1]
    dp_RDO $server1 -callback "set RDO2" set a rpc3.9d
    set rv2 [catch {dp_RPC $server1 set a rpc3.9e} msg2]
    list $rv1 $rv2 $RDO1 $RDO2 $msg1 $msg2
} [list 1 0 rpc3.9c rpc3.9d "dp_RPC timed out on $server1" rpc3.9e]


#------------------------------------------------------------------------------
#
# Shutdown protocol tests
#
test rpc-4.1 {proper closing one end of an RPC} {
    catch {close $server1}
} 0

test rpc-4.2 {check dp_atexit list} {
    dp_atexit list
} [list "close $server2"]

test rpc-4.3 {proper closing another end of an RPC} {
    set remFile [dp_RPC $server2 set rpcFile]
    catch {dp_RDO $server2 close $remFile}
} 0

test rpc-4.4 {improper closing another end of an RPC} {
    set f [dp_MakeRPCClient $hostname $S_PORT]
    set remFile [dp_RPC $f set rpcFile]
    list [catch {dp_RPC $f close $remFile} msg] $msg
} [list 1 [format {file "%s" isn't open caught while receiving RPC
-- Closing connection} $server1]]

catch {close $server2}

test rpc-4.5 {check dp_atclose list} {
    list [dp_atclose $server1 list] [dp_atclose $server2 list]
} {{} {}}

dp_MakeRPCClient $hostname $S_PORT

test rpc-4.6 {check server crashing} {
    list [catch {dp_RPC $server1 exit} msg] $msg
} [list 1 [format {file "%s" isn't open caught while receiving RPC
-- Closing connection} $server1]]

test rpc-4.7 {check number 2 of server closure} {
    list [catch {dp_MakeRPCClient $hostname $S_PORT} msg] $msg
} [list 1 "Couldn't open connection to $hostname:$S_PORT : Connection refused"]

test rpc-4.8 {check server crashing} {
    list [catch {close $server1} msg] $msg
} [list 1 [format {file "%s" isn't open} $server1]]
