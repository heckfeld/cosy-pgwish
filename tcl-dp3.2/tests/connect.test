if {[string compare test [info procs test]] == 1} then {source defs}

proc ignore args {}

set hostname [exec hostname]
proc MakeInetAddr {name} {
    set addr [dp_address create $name 0]
    set x [dp_address info $addr]
    dp_address delete $addr
    lindex $x 0
}

#------------------------------------------------------------------------
#
# Unix domain socket tests
#
test connect-1.0 {test of internet server connect} {
    catch {dp_connect -server 0} rv
    set port [lindex $rv 1]
    set afile [lindex $rv 0]
    string match file* $afile
} 1

test connect-1.1 {test of connecting to previously made server} {
    set sfile [lindex [dp_connect $hostname $port] 0]
    string match file* $sfile
} 1

test connect-1.2 {test of accepting connection} {
    set rv [dp_accept $afile]
    set cfile [lindex $rv 0]
    list [string match file* $cfile] [lindex $rv 1]
} [list 1 [MakeInetAddr $hostname]]

test connect-1.3 {test of closing connection} {
    list [catch {close $afile}] [catch {close $sfile}] [catch {close $cfile}]
} {0 0 0}

#------------------------------------------------------------------------
#
# Unix domain socket tests
#
test connect-2.0 {test of unix server connect} {
    set afile [dp_connect -server ~/socket]
    string match file* $afile
} 1

test connect-2.1 {test of connecting to previously made unix server} {
    set sfile [lindex [dp_connect ~/socket] 0]
    string match file* $sfile
} 1

test connect-2.2 {test of accepting connection} {
    set rv [dp_accept $afile]
    set cfile [lindex $rv 0]
    list [string match file* $cfile] [lindex $rv 1]
} {1 0.0.0.0}

#------------------------------------------------------------------------
#
# Send/recv tests
#
test connect-3.0 {test of basic single packet send utility} {
    catch {dp_packetSend $sfile test}
} 0

test connect-3.1 {test of basic single packet receive utility} {
    dp_packetReceive $cfile
} test

test connect-3.2 {test of multiple packet send utility} {
    list [dp_packetSend $sfile test1] \
	 [dp_packetSend $sfile test2] \
	 [dp_packetSend $sfile test3] \
	 [dp_packetSend $sfile test4] \
	 [dp_packetSend $sfile test5]
} {5 5 5 5 5}

test connect-3.3 {test of isreadable} {
    lindex [dp_isready $cfile] 0
} 1

test connect-3.4 {test of multiple packet receive utility} {
    list [dp_packetReceive $cfile]	\
	 [dp_packetReceive $cfile]	\
	 [dp_packetReceive $cfile]	\
	 [dp_packetReceive $cfile]	\
	 [dp_packetReceive $cfile]
} {test1 test2 test3 test4 test5}

#------------------------------------------------------------------------
#
# Closing unix domain socket tests
#
test connect-4.0 {test of closing unix sockets} {
    list [catch {close $afile}] [catch {close $sfile}] [catch {close $cfile}]
} {0 0 0}

exec rm [glob ~/socket]

